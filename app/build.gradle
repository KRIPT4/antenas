apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "ar.com.lichtmaier.antenas"
        minSdkVersion 9
        targetSdkVersion 23
        versionName "2.0.3"
        resConfigs "en", "en-rGB", "en-rUS", "en-rAU", "en-rCA", "en-rNZ", "es", "es-rUS", "fr", "fr-rCA", "pt", "pt-rBR", "pt-rPT"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    flavorDimensions "level", "device"

    productFlavors {
        v9 {
            dimension "level"
            versionCode 902003
        }
        v8 {
            dimension "level"
            versionCode 802003
            minSdkVersion 8
        }
        phone {
            dimension "device"
        }
        tv {
            dimension "device"
            versionCode 2102003
            minSdkVersion 21
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("v8") && names.contains("tv")) {
            variant.ignore = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
    //noinspection GradleDependency
    v8Compile 'com.google.android.gms:play-services:3.2.65'
    v9Compile 'com.google.android.gms:play-services-location:9.0.0'
    v9Compile 'com.google.android.gms:play-services-maps:9.0.0'
    v9Compile 'com.google.android.gms:play-services-ads:9.0.0'
    v9Compile 'com.google.android.gms:play-services-analytics:9.0.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.github.davidmoten:geo:0.7.4'
    compile files('libs/geodesy.jar')
    compile 'com.google.maps.android:android-maps-utils:0.4.1'
    tvCompile 'com.android.support:leanback-v17:23.4.0'
}
